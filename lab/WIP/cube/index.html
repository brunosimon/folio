<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <title>Three JS - Shaders - Serge</title>
    <style>
        body,html {width:100%;height:100%;margin:0;padding:0;}
        canvas {position:absolute;top:0;left:0;width:100% !important;height:100% !important;}
    </style>
</head>
<body>

    <script id="shader-vertex-1" type="x-shader/x-vertex">

        int face(vec3 coord)
        {
            if((abs(coord.x) > abs(coord.y)) && (abs(coord.x) > abs(coord.z)))
            {
                if (coord.x > 0.0)
                    return 0;
                else
                    return 1;
            }

            if(abs(coord.y) > abs(coord.z))
            {
                if (coord.y > 0.0)
                    return 2;
                else
                    return 3;
            }

            if(coord.z > 0.0)
                return 4;

            return 5;
        }

        vec3 cube(vec3 position,vec3 cubeOrigin)
        {
            float CUBE_SIZE = 1.0;
            vec3 coord      = normalize(position - cubeOrigin);
            int face        = face(coord);
            vec3 faceNormal = vec3(0);

            if(face == 0)
                faceNormal = vec3(1,0,0);
            else if(face == 1)
                faceNormal = vec3(-1,0,0);
            else if(face == 2)
                faceNormal = vec3(0,1,0);
            else if(face == 3)
                faceNormal = vec3(0,-1,0);
            else if(face == 4)
                faceNormal = vec3(0,0,1);
            else if(face == 5)
                faceNormal = vec3(0,0,-1);

            float gamma = acos(dot(faceNormal,coord));
            float alfa = 3.14 - 1.57 - gamma; //there is no PI or HALF_PI in glsl you have to define it on your own.
            //this is little hard stuff. You want to know length from origin to place on cube. It is simple triangle and can be solved by law of sines.
            //It should be multiplied with sin(HALF_PI) but it is 1.
            float posFactor = (CUBE_SIZE / 2.0) / sin(alfa);

            return (cubeOrigin + coord * posFactor);
        }

        //
        // GLSL textureless classic 3D noise "cnoise",
        // with an RSL-style periodic variant "pnoise".
        // Author:  Stefan Gustavson (stefan.gustavson@liu.se)
        // Version: 2011-10-11
        //
        // Many thanks to Ian McEwan of Ashima Arts for the
        // ideas for permutation and gradient selection.
        //
        // Copyright (c) 2011 Stefan Gustavson. All rights reserved.
        // Distributed under the MIT license. See LICENSE file.
        // https://github.com/ashima/webgl-noise
        //

        vec3 mod289(vec3 x)
        {
          return x - floor(x * (1.0 / 289.0)) * 289.0;
        }

        vec4 mod289(vec4 x)
        {
          return x - floor(x * (1.0 / 289.0)) * 289.0;
        }

        vec4 permute(vec4 x)
        {
          return mod289(((x*34.0)+1.0)*x);
        }

        vec4 taylorInvSqrt(vec4 r)
        {
          return 1.79284291400159 - 0.85373472095314 * r;
        }

        vec3 fade(vec3 t) {
          return t*t*t*(t*(t*6.0-15.0)+10.0);
        }

        // Classic Perlin noise, periodic variant
        float pnoise(vec3 P, vec3 rep)
        {
          vec3 Pi0 = mod(floor(P), rep); // Integer part, modulo period
          vec3 Pi1 = mod(Pi0 + vec3(1.0), rep); // Integer part + 1, mod period
          Pi0 = mod289(Pi0);
          Pi1 = mod289(Pi1);
          vec3 Pf0 = fract(P); // Fractional part for interpolation
          vec3 Pf1 = Pf0 - vec3(1.0); // Fractional part - 1.0
          vec4 ix = vec4(Pi0.x, Pi1.x, Pi0.x, Pi1.x);
          vec4 iy = vec4(Pi0.yy, Pi1.yy);
          vec4 iz0 = Pi0.zzzz;
          vec4 iz1 = Pi1.zzzz;

          vec4 ixy = permute(permute(ix) + iy);
          vec4 ixy0 = permute(ixy + iz0);
          vec4 ixy1 = permute(ixy + iz1);

          vec4 gx0 = ixy0 * (1.0 / 7.0);
          vec4 gy0 = fract(floor(gx0) * (1.0 / 7.0)) - 0.5;
          gx0 = fract(gx0);
          vec4 gz0 = vec4(0.5) - abs(gx0) - abs(gy0);
          vec4 sz0 = step(gz0, vec4(0.0));
          gx0 -= sz0 * (step(0.0, gx0) - 0.5);
          gy0 -= sz0 * (step(0.0, gy0) - 0.5);

          vec4 gx1 = ixy1 * (1.0 / 7.0);
          vec4 gy1 = fract(floor(gx1) * (1.0 / 7.0)) - 0.5;
          gx1 = fract(gx1);
          vec4 gz1 = vec4(0.5) - abs(gx1) - abs(gy1);
          vec4 sz1 = step(gz1, vec4(0.0));
          gx1 -= sz1 * (step(0.0, gx1) - 0.5);
          gy1 -= sz1 * (step(0.0, gy1) - 0.5);

          vec3 g000 = vec3(gx0.x,gy0.x,gz0.x);
          vec3 g100 = vec3(gx0.y,gy0.y,gz0.y);
          vec3 g010 = vec3(gx0.z,gy0.z,gz0.z);
          vec3 g110 = vec3(gx0.w,gy0.w,gz0.w);
          vec3 g001 = vec3(gx1.x,gy1.x,gz1.x);
          vec3 g101 = vec3(gx1.y,gy1.y,gz1.y);
          vec3 g011 = vec3(gx1.z,gy1.z,gz1.z);
          vec3 g111 = vec3(gx1.w,gy1.w,gz1.w);

          vec4 norm0 = taylorInvSqrt(vec4(dot(g000, g000), dot(g010, g010), dot(g100, g100), dot(g110, g110)));
          g000 *= norm0.x;
          g010 *= norm0.y;
          g100 *= norm0.z;
          g110 *= norm0.w;
          vec4 norm1 = taylorInvSqrt(vec4(dot(g001, g001), dot(g011, g011), dot(g101, g101), dot(g111, g111)));
          g001 *= norm1.x;
          g011 *= norm1.y;
          g101 *= norm1.z;
          g111 *= norm1.w;

          float n000 = dot(g000, Pf0);
          float n100 = dot(g100, vec3(Pf1.x, Pf0.yz));
          float n010 = dot(g010, vec3(Pf0.x, Pf1.y, Pf0.z));
          float n110 = dot(g110, vec3(Pf1.xy, Pf0.z));
          float n001 = dot(g001, vec3(Pf0.xy, Pf1.z));
          float n101 = dot(g101, vec3(Pf1.x, Pf0.y, Pf1.z));
          float n011 = dot(g011, vec3(Pf0.x, Pf1.yz));
          float n111 = dot(g111, Pf1);

          vec3 fade_xyz = fade(Pf0);
          vec4 n_z = mix(vec4(n000, n100, n010, n110), vec4(n001, n101, n011, n111), fade_xyz.z);
          vec2 n_yz = mix(n_z.xy, n_z.zw, fade_xyz.y);
          float n_xyz = mix(n_yz.x, n_yz.y, fade_xyz.x);
          return 2.2 * n_xyz;
        }

        uniform samplerCube texCube;
        uniform   float time;
        attribute vec3  new_color;
        varying   vec3  v3Normal;
        varying   vec3  v3Direction;

        vec3 get_new_position()
        {
            vec3 new_position = position;

            // Set up
            float global_frequency  = 8.0;
            float details_frequency = 44.0;
            float global_strength   = 0.02;
            float details_strength  = 5.0;
            float time_frequency    = 2000.0;
            float effect_limit      = 0.6;

            // Current face
            int face = face(position);

            // Time influence
            vec3 time_incluence = vec3(0.0,0.0,0.0);

            if(face == 0 || face == 1)
                time_incluence.x = time / time_frequency;
            else if(face == 2 || face == 3)
                time_incluence.y = time / time_frequency;
            else if(face == 4 || face == 5)
                time_incluence.z = time / time_frequency;

            // First global perlin (from 0 to 1)
            float global_perlin = (pnoise(global_frequency * position + time_incluence, vec3(101.0)) + 1.0) / 2.0;

            // Details perlin (from 0 to 1)
            float details_perlin = (pnoise(details_frequency * position + time_incluence, vec3(103.0)) + 1.0) / 2.0;

            // Modify
            if(
                global_perlin > effect_limit &&
                (
                    ((face == 0 || face == 1) && abs(position.z) < 0.499 && abs(position.y) < 0.499) ||
                    ((face == 2 || face == 3) && abs(position.z) < 0.499 && abs(position.x) < 0.499) ||
                    ((face == 4 || face == 5) && abs(position.x) < 0.499 && abs(position.y) < 0.499)
                )
            )
            {
                float global_displacement = (global_perlin - effect_limit) * (global_strength / effect_limit);
                new_position += normal * global_displacement;
                new_position += normal * (details_perlin * global_displacement * details_strength);
            }

            return new_position;
        }

        void main()
        {
            vec3 new_position = get_new_position();

            float ratio = (length(new_position) - 0.9) * 3.0;
            ratio = clamp(ratio,0.0,1.0);

            v3Normal    = normalMatrix * normal;
            v3Direction = cameraPosition - position;

            gl_Position = projectionMatrix * modelViewMatrix * vec4( new_position, 1. );
        }

    </script>

    <script id="shader-fragment-1" type="x-shader/x-fragment">

        uniform samplerCube texCube;
        varying vec3 v3Normal;
        varying vec3 v3Direction;

        void main()
        {
            vec4 cubeColor = textureCube( texCube, reflect( v3Direction, v3Normal ) );
            gl_FragColor = cubeColor;
            // gl_FragColor = vec4(1.0,0.0,0.0,1.0);
        }

    </script>

    <canvas id="canvas"></canvas>

    <script src="src/js/libs/three.70.min.js"></script>
    <script src="src/js/script.js"></script>
</body>
</html>
